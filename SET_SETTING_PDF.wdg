#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : SET_SETTING_PDF
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x1a95629c00b8b9df
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes :
   -
     internal_properties : CAAAAAgAAADxw5Qf88SdltFqh4gsJ0ZfMWtuLAZ3h4tJMIumiFVJyVtWmFglQnXhkvnPFsxyC5yB/FgUhlo5/Y3U9+jFiwdrwgobbk4TG31XKdHfY6WpechCu5LXFk1s57+EP0SkFMeWglbMCzGUtLoczZY=
     code : |1-
      
      // Structure to store the additional information of each file
      STAdditionalInformation is Structure
      	FullPath is string <Serialize = "path"> // Full path of the file
      	DisplayMode is int <Serialize = "mode"> // Display mode (1 page, 2 pages, ...)
      	Zoom is int <Serialize = "zoom"> // Zoom level
      end
      
      // Array containing the additional file information
      garrAdditionalInformation is array of STAdditionalInformation
      
      // Loads the information
      AdditionalInformationLoad()
      
      // Indicates whether the display parameters (mode, zoom, ...) are restored when re-opening a document
      gbRestoreDisplayParameters is boolean = LoadParameter("RestoreDisplay", False)
     type : 720896
   -
     code : |1+
      
      // Saves the information
      AdditionalInformationSave()
     type : 720898
  procedures :
   -
     name : AdditionalInformationLoad
     internal_properties : CAAAAAgAAAAiI/31k/72nQ7OP1hH/WMeGHUqdVrnwUiFrxqUaDXAL2bv9WlPWxk4ofILOZA0llHEz7FQeiwD5nFpJkjN7MvOyykx5sa8YfCZUijDIdyhJjWa+dBDbkEiEPpfeIH0gbjlysiLIPXmrdouBp35
     procedure_id : 1915545638682606047
     type_code : 15
     code : |1-
      // Summary: Loads the additional information stored
      // Syntax:
      //AdditionalInformationLoad ()
      //
      // Parameters:
      //	None
      // Return value:
      // 	None
      //
      Procédure AdditionalInformationLoad()
      
      // Defines the file path
      sFile is string = fDataDirUser() + [fSep] + "pdf.json"
      
      // Clear the table 
      garrAdditionalInformation.DeleteAll()
      
      // Checks the file existence
      if fFileExist( sFile ) = False then return
      
      // Loads the data
      sJSON is string = fLoadText( sFile )
      
      // If the file is empty, no action
      if sJSON = "" then return
      
      // Deserializes the content
      Deserialize(garrAdditionalInformation, sJSON, psdJSON)
      
      // Deletes the oldest entries
      while garrAdditionalInformation..Count > 100
      	garrAdditionalInformation.Delete( LastElement )
      end
     type : 458752
   -
     name : AdditionalInformationSave
     procedure_id : 1915545638682671583
     type_code : 15
     code : |1+
      // Summary: Saves the additional information
      // Syntax:
      //AdditionalInformationSave ()
      //
      // Parameters:
      //	None
      // Return value:
      // 	None
      //
      Procédure AdditionalInformationSave()
      
      sJSON is string ANSI
      
      // Defines the file path
      sFile is string = fDataDirUser() + [fSep] + "pdf.json"
      
      // Serializes the data
      Serialize(garrAdditionalInformation, sJSON, psdJSON)
      
      // Local backup
      fSaveText( sFile, sJSON)
     type : 458752
   -
     name : AdditionalInformationAdd
     procedure_id : 1915545638682737119
     type_code : 15
     code : |1+
      // Summary: Adds additional information for a file
      // Syntax:
      //AdditionalInformationAdd (<sPath> is string, <nDisplayMode> is int, <nZoom> is int)
      //
      // Parameters:
      //	sPath (ANSI string): Full file path
      //	nDisplayMode (integer): Display mode
      //	nZoom (integer): Zoom
      // Return value:
      // 	None
      //
      Procédure AdditionalInformationAdd(local sPath is string, local nDisplayMode is int, local nZoom is int)
      
      // Checks whether this document is already found in the list
      nSubscript is int = Seek( garrAdditionalInformation, asLinear + tccIgnoreCase, "FullPath", sPath )
      
      if nSubscript > 0 then
      	
      	// Update the information
      	garrAdditionalInformation[ nSubscript ].DisplayMode = nDisplayMode
      	garrAdditionalInformation[ nSubscript ].Zoom = nZoom
      	
      else
      	
      	// Add some information
      	garrAdditionalInformation.Add( [ sPath, nDisplayMode, nZoom ] )		
      	
      end
      
      // Saves the table
      AdditionalInformationSave()
     type : 458752
   -
     name : AdditionalInformationGet
     procedure_id : 1915545638682802655
     type_code : 15
     code : |1+
      // Summary: Gets additional information for a file
      // Syntax:
      //[ <Result> = ] AdditionalInformationGet (<sPath> is string)
      //
      // Parameters:
      //	sPath (ANSI string): Full file path
      // Return value:
      // 	multi-value: Display mode, zoom level
      
      Procédure AdditionalInformationGet(local sPath is string) : (int, int)
      
      // Checks whether this document is found in the list
      nSubscript is int = Seek( garrAdditionalInformation, asLinear + tccIgnoreCase, "FullPath", sPath )
      if nSubscript <= 0 then result (-1, -1)
      
      // Returns the stored information
      result ( garrAdditionalInformation[nSubscript].DisplayMode, garrAdditionalInformation[nSubscript].zoom )
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_SansNom1
resources :
 string_res :
  identifier : 0x1a9521bc02b94f5c
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
